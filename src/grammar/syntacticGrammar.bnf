program        -> statement* EOF ;
declaration    -> classDecl
                | varDecl
                | statement
                | errorStmt ;
classDecl      -> "class" IDENTIFIER "{" property* "}" ;
varDecl        -> "var" property ;
typeDecl       -> "type" IDENTIFIER ( "[" parameters "]" )? ; "=" typeExpr ;
statement      -> exprStmt
                | ifStmt
                | returnStmt
                | whileStmt
                | breakStmt
                | continueStmt
                | block ;
exprStmt       -> expression ;
ifStmt         -> "if" "(" expression ")" statement ( "else" statement )? ;
returnStmt     -> "return" expression ;
whileStmt      -> "while" "(" expression ( ";" expression )? ")" statement ;
breakStmt      -> "break" ;
continueStmt   -> "continue" ;
block          -> "{" declaration* "}" ;
expression     -> assignment ;
assignment     -> ( call "." )? IDENTIFIER "=" assignment
                | ternary ;
ternary        -> logic_or ( "?" ternary ":" ternary )? ;
logic_or       -> logic_and ( "or" logic_and )* ;
logic_and      -> equality ( "and" equality )* ;
equality       -> comparison ( ( "!=" | "==" ) comparison )* ;
comparison     -> term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term           -> factor ( ( "-" | "+" | "#" ) factor )* ;
factor         -> unary ( ( "/" | "*" ) unary )* ;
unary          -> ( "!" | "-" ) unary | call ;
call           -> primary ( ( "[" generics "]" )? "(" expressions? ")" | "." accessor )* ;
primary        -> NUMBER | STRING | "true" | "false" | "null" | "this" | IDENTIFIER | function | list | record | "(" expression ")" | error ;
function       -> "f" "(" parameters? ")" block ;
list           -> "[" expressions* "]" ;
record         -> "{" entries* "}" ;
error          -> ( "or" ) logic_or
                | ( "and" ) logic_and
                | ( "!=" | "==" ) equality
                | ( ">" | ">=" | "<" | "<=" ) comparison
                | ( "+" | "#" ) term
                | ( "/" | "*" ) factor ;
                | ( ";" ) ;
errorStmt      -> "break" | "continue" | "class" | "var" | "for" | "if" | "while" | "return";

entries        -> entry ( "," entry )* ;
entry          -> STRING ":" expression ;
expressions    -> expression ( "," expression )* ;
property       -> IDENTIFIER ( ":" typeExpr )? "=" expression ;
parameters     -> IDENTIFIER ( "," IDENTIFIER )* ;


typeExpr          -> compositeTypeExpr ;
compositeTypeExpr -> subTypeExpr ( ( "|" | "&" ) subTypeExpr )* ;
subTypeExpr       -> callableType | objectTypeType | genericType | IDENTIFIER ;
callableTypeExpr  -> "(" generics ")" "->" typeExpr ;
objectTypeExpr    -> "{" typeEntries* "}" ;
genericType       -> IDENTIFIER ( "[" generics "]" )? ;

generics          -> typeExpr ( "," typeExpr )* ;
typeEntries       -> typeEntry ( "," typeEntry )* ;
typeEntry         -> IDENTIFIER ":" typeExpr ;