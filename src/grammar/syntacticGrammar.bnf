program        -> statement* EOF ;
declaration    -> classDecl
                | varDecl
                | statement
                | errorStmt ;
classDecl      -> "class" IDENTIFIER "{" field* "}" ;
varDecl        -> "var" field ;
statement      -> exprStmt
                | ifStmt
                | returnStmt
                | whileStmt
                | breakStmt
                | continueStmt
                | block ;
exprStmt       -> expression ";" ;
ifStmt         -> "if" "(" expression ")" statement ( "else" statement )? ;
returnStmt     -> "return" expression ";" ;
whileStmt      -> "while" "(" expression ( ";" expression )? ")" statement ;
breakStmt      -> "break" ";" ;
continueStmt   -> "continue" ";" ;
block          -> "{" declaration* "}" ;
expression     -> assignment ;
assignment     -> ( call "." )? IDENTIFIER "=" assignment
                | ternary ;
ternary        -> logic_or ( "?" ternary ":" ternary )? ;
logic_or       -> logic_and ( "or" logic_and )* ;
logic_and      -> equality ( "and" equality )* ;
equality       -> comparison ( ( "!=" | "==" ) comparison )* ;
comparison     -> term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term           -> factor ( ( "-" | "+" | "#" ) factor )* ;
factor         -> unary ( ( "/" | "*" ) unary )* ;
unary          -> ( "!" | "-" ) unary | call ;
call           -> primary ( "(" arguments? ")" | "." IDENTIFIER )* ;
primary        -> NUMBER | STRING | "true" | "false" | "null" | IDENTIFIER | function | "(" expression ")" | error ;
function       -> "f" "(" parameters? ")" block ;
arguments      -> expression ( "," expression )* ;
error          -> ( "or" ) logic_or
                | ( "and" ) logic_and
                | ( "!=" | "==" ) equality
                | ( ">" | ">=" | "<" | "<=" ) comparison
                | ( "+" | "#" ) term
                | ( "/" | "*" ) factor ;
errorStmt      -> "break" | "continue" | "class" | "var" | "for" | "if" | "while" | "return";

field          -> IDENTIFIER "=" expression ";"? ;
parameters     -> parameter ( "," parameter )* ;
parameter      -> IDENTIFIER ;